###
0000000     0000000    0000000   00000000   0000000  
000   000  000   000  000   000  000   000  000   000
0000000    000   000  000000000  0000000    000   000
000   000  000   000  000   000  000   000  000   000
0000000     0000000   000   000  000   000  0000000  
###

{ elem, randIntRange } = require 'kxk'

function Board

    @: (parent) ->
        
        @div = elem 'div' class:'board'   parent:parent
        @img = elem 'img' class:'wood'    src:'../img/wood.png' parent:@div
        @shd = elem 'div' class:'shadows' parent:@div
        
        br = @div.getBoundingClientRect()
        w = parseInt br.width
        h = parseInt br.height
        
        @height = h*2
        @width  = @height
        s = @height
        
        @canvas = elem 'canvas' class:'lines' height:s, width:s, parent:@div

        @ctx = @canvas.getContext '2d'
        @ctx.strokeStyle = 'black'
        @ctx.lineWidth = 4
        @ctx.lineCap = "round"

        @ctx.fillStyle = '#f6c67111'
        @ctx.rect 0,0,s,s
        @ctx.fill()
        
        @ctx.fillStyle = 'black'
        
        d = s/20
        o = d
        
        for i in 0...19
            @ctx.beginPath()
            @ctx.moveTo o+i*d, o
            @ctx.lineTo o+i*d, s-o
            @ctx.moveTo o, o+i*d
            @ctx.lineTo s-o, o+i*d
            @ctx.stroke()   
            
        for x in -1..1
            for y in -1..1
                @ctx.beginPath()
                @ctx.arc s/2+x*d*6, s/2+y*d*6, d/12, 0, 2 * Math.PI, false
                @ctx.fill()
                
        for x in 0..18
            for y in 0..18
              @addStone [x,y], (x+y)%2
            x+=1
                
    addStone: (pos, black) ->
        
        d = 100/20
        src = "../img/stone_white#{randIntRange 1 15}.png"
        src = '../img/stone_black.png' if black
        shadow = elem 'img' class:'shadow' src:'../img/stone_shadow.png', width:"auto", height:"#{d+1}%" parent:@shd
        stone = elem 'img' class:'stone' src:src, width:"auto", height:"#{d}%" parent:@div
        x = (pos[0]+0.5)*100/20
        y = (pos[1]+0.5)*100/20
        stone.style = "left:#{x}%; top:#{y}%;" 
        shadow.style = "left:#{x}%; top:#{y}%;" 

module.exports = Board
