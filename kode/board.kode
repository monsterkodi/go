###
0000000     0000000    0000000   00000000   0000000  
000   000  000   000  000   000  000   000  000   000
0000000    000   000  000000000  0000000    000   000
000   000  000   000  000   000  000   000  000   000
0000000     0000000   000   000  000   000  0000000  
###

kxk = require 'kxk'
{ max, min } = Math
{ elem, kpos, post, randIntRange, $ } = kxk
{ stone, stoneColor, alpha, opponent } = require './util/util'

function Board

    @: (parent, @size=19) ->
        
        @div = elem 'div' class:'board'     parent:parent
        @img = elem 'img' class:'wood'      src:'../img/board.png' parent:@div
        # @blk = elem 'div' class:'blank'     parent:@div
        @shd = elem 'div' class:'shadows'   parent:@div
        @lgd = elem 'div' class:'legends'   parent:@div
        
        @div.addEventListener 'mousemove'  @onMouseMove
        @div.addEventListener 'mousedown'  @onMouseDown
        @div.addEventListener 'mouseleave' @onMouseLeave
        
        @height = 1000
        @width  = @height
        s = @height
        d = s/(@size+1)
        h = 90/(@size+1)

        @lines()
        @legend()
        
        @stn = elem 'div' class:'stones'      parent:@div
        @lib = elem 'div' class:'liberties'   parent:@div
        @num = elem 'div' class:'numbers'     parent:@div
        @trr = elem 'div' class:'territory'   parent:@div
        @hlt = elem 'div' class:'highlts'     parent:@div
        @hvw = elem 'div' class:"hover white" parent:@hlt
        @hvb = elem 'div' class:"hover black" parent:@hlt
        @hvw.style = "width:#{h}%; height:#{h}%; display:none;" 
        @hvb.style = "width:#{h}%; height:#{h}%; display:none;" 
        @hvr = white:@hvw, black:@hvb
        
        @num.style.display = window.stash.get('numbers') ? 'initial' : 'none'
        @lib.style.display = window.stash.get('liberties') ? 'initial' : 'none'
        
        @showTerritory = window.stash.get 'territory'
        @trr.style.display = window.stash.get('territory') ? 'initial' : 'none'
        
        @lst = elem 'div' class:"last" parent:@hlt
        @lst.style = "display:none;"
     
        post.on 'variation' @onVariation
        post.on 'resize' @onResize
                
    onResize: =>
        
        @annotate()
        
    # 000      000  000   000  00000000   0000000  
    # 000      000  0000  000  000       000       
    # 000      000  000 0 000  0000000   0000000   
    # 000      000  000  0000  000            000  
    # 0000000  000  000   000  00000000  0000000   
    
    lines: ->
        
        s = @height
        d = s/(@size+1)
        o = d
        
        @canvas = elem 'canvas' class:'lines' height:s, width:s, parent:@div
        @ctx = @canvas.getContext '2d'
        @ctx.strokeStyle = 'black'
        @ctx.lineWidth = @size == 19 ? 2 : @size == 13 ? 2.5 : 3
        @ctx.lineCap = "round"

        @ctx.fillStyle = '#f6c67111'
        @ctx.rect 0,0,s,s
        @ctx.fill()
        
        @ctx.fillStyle = 'black'
                
        for i in 0...@size
            @ctx.beginPath()
            @ctx.moveTo o+i*d, o
            @ctx.lineTo o+i*d, s-o
            @ctx.moveTo o, o+i*d
            @ctx.lineTo s-o, o+i*d
            @ctx.stroke()   
            
        if @size >= 9
            for x in -1..1
                for y in -1..1
                    @ctx.beginPath()
                    if @size >= 13
                        @ctx.arc s/2+x*d*(int(-@size/2) + 3), s/2+y*d*(int(-@size/2) + 3), s/180, 0, 2 * Math.PI, false
                    else if x and y
                        @ctx.arc s/2+x*d*(int(-@size/2) + 2), s/2+y*d*(int(-@size/2) + 2), s/180, 0, 2 * Math.PI, false
                    @ctx.fill()
                
    # 000      00000000   0000000   00000000  000   000  0000000    
    # 000      000       000        000       0000  000  000   000  
    # 000      0000000   000  0000  0000000   000 0 000  000   000  
    # 000      000       000   000  000       000  0000  000   000  
    # 0000000  00000000   0000000   00000000  000   000  0000000    
    
    legend: ->
        
        d = 100/(@size+1)
        
        for x in 0...@size
            
            n = elem 'div' class:'legend' text:alpha[x], parent:@lgd
            n.style.left = "#{d*(x+1)}%"
            n.style.top = "#{d/4}%"
            
            n = elem 'div' class:'legend' text:alpha[x], parent:@lgd
            n.style.left = "#{d*(x+1)}%"
            n.style.top = "#{100-d/4}%"

            n = elem 'div' class:'legend' text:@size-x, parent:@lgd
            n.style.left = "#{d/4}%"
            n.style.top = "#{d*(x+1)}%"
            
            n = elem 'div' class:'legend' text:@size-x, parent:@lgd
            n.style.left = "#{100-d/4}%"
            n.style.top = "#{d*(x+1)}%"
            
            @lgd.style.display = window.stash.get('legend') ? 'initial' : 'none'
         
    # 000000000   0000000    0000000    0000000   000      00000000  
    #    000     000   000  000        000        000      000       
    #    000     000   000  000  0000  000  0000  000      0000000   
    #    000     000   000  000   000  000   000  000      000       
    #    000      0000000    0000000    0000000   0000000  00000000  
    
    toggleNumbers: ->
        
        window.stash.set 'numbers' not window.stash.get 'numbers'
        @num.style.display = window.stash.get('numbers') ? 'initial' : 'none'
        # if window.stash.get('numbers') and window.stash.get 'liberties'
            # @toggleLiberties()
            
    toggleLiberties: ->
        
        window.stash.set 'liberties' not window.stash.get 'liberties'
        @lib.style.display = window.stash.get('liberties') ? 'initial' : 'none'
        # if window.stash.get('liberties') and window.stash.get 'numbers'
            # @toggleNumbers()

    toggleLegend: ->
        
        window.stash.set 'legend' not window.stash.get 'legend'            
        @lgd.style.display = window.stash.get('legend') ? 'initial' : 'none'

    toggleTerritory: ->

        @showTerritory = not @showTerritory
        window.stash.set 'territory' @showTerritory
        @trr.style.display = @showTerritory ? 'initial' : 'none'
        
    # 00     00   0000000   000   000   0000000  00000000  
    # 000   000  000   000  000   000  000       000       
    # 000000000  000   000  000   000  0000000   0000000   
    # 000 0 000  000   000  000   000       000  000       
    # 000   000   0000000    0000000   0000000   00000000  
    
    onMouseLeave: (event) =>
        
        @hvr.black.style.display = 'none'
        @hvr.white.style.display = 'none'
        
    onMouseMove: (event) =>
        
        c = @posAtEvent event
        
        @hvr.white.style.display = 'none'
        @hvr.black.style.display = 'none'
        
        nextColor = @game.nextColor()

        return if @game.players[nextColor] != 'human'
        return if c.x < 0 or c.y < 0 or c.x >= @size or c.y >= @size
        return if not @game.legal nextColor, [c.x, c.y]
        
        hvr = @hvr[nextColor]
        hvr.style.display = 'initial'
        
        p = @posToPrcnt c
        
        hvr.style.left = "#{p.x}%"
        hvr.style.top  = "#{p.y}%"
        hvr.style.borderRadius = "#{0.5*@div.getBoundingClientRect().height/(@size+1)}px"
        
    onMouseDown: (event) =>

        nextColor = @game.nextColor()
        return if @game.players[nextColor] != 'human'
        
        c = @posAtEvent event
        if @game
            p = @game.pos [c.x, c.y]
            if p in @game.all_legal()
                @hvr[nextColor].style.display = 'none'
                post.emit 'playerMove' p, 'human'
        
    # 000       0000000    0000000  000000000  
    # 000      000   000  000          000     
    # 000      000000000  0000000      000     
    # 000      000   000       000     000     
    # 0000000  000   000  0000000      000     
    
    showLastMove: ->
        
        if @game.start() or @game.moves.lastIsPass()
            @lst.style.display = 'none'
            return
            
        r = @coordToPrcnt @game.lastCoord()
        # log 'showLastMove' r, @game.lastPos(), @game.lastColor()
        d = @div.getBoundingClientRect().height/(@size+1)
        @lst.style.left         = "#{r.x}%"
        @lst.style.top          = "#{r.y}%"
        @lst.style.width        = "#{0.5*d}px"
        @lst.style.height       = "#{0.5*d}px"
        @lst.style.borderRadius = "#{0.5*d}px"
        @lst.style.borderWidth  = "#{0.1*d}px"
        @lst.style.display      = 'initial'
        @lst.classList.remove 'black'
        @lst.classList.remove 'white'
        @lst.classList.add @game.nextColor()
        
    posAtEvent: (event) ->
        
        p = kpos event
        b = @div.getBoundingClientRect()
        p.sub b
        s = b.width / (@size+1)
        p.div kpos s, s
        p.add kpos -1 -1
        kpos int(Math.round(p.x)), int(Math.round(p.y))
        
    coordToPrcnt: (c) ->

        @posToPrcnt kpos c[0], c[1]

    posToPrcnt: (c) ->

        kpos 100 * (c.x+1) / (@size+1), 100 * (c.y+1) / (@size+1)
                
    #  0000000  000000000   0000000   000   000  00000000  
    # 000          000     000   000  0000  000  000       
    # 0000000      000     000   000  000 0 000  0000000   
    #      000     000     000   000  000  0000  000       
    # 0000000      000      0000000   000   000  00000000  
        
    delStone: (c) ->

        while s = $ ".pos#{c[0]}_#{c[1]}"
            s.remove()
            
    addStone: (c, s=stone.black) ->
        
        d = 100/(@size+1)
        cn = stoneColor[s]
        cn += 1+(c[0]+c[1])%15 if cn == 'white'
        x = (c[0]+0.5)*100/(@size+1)
        y = (c[1]+0.5)*100/(@size+1)
        shd = elem 'img' class:"shadow pos#{c[0]}_#{c[1]}" src:'../img/shadow_hard.png' width:'auto' height:"#{1.15*d}%" style:"left:#{x}%; top:#{y}%;" parent:@shd
        stn = elem 'img' class:"stone pos#{c[0]}_#{c[1]}"  src:"../img/stone_#{cn}.png" width:'auto' height:"#{d}%"      style:"left:#{x}%; top:#{y}%;" parent:@stn
        
    clear: ->
        
        @lst.style.display = 'none'
        @trr.innerHTML = ''
        @num.innerHTML = ''
        @lib.innerHTML = ''
        @stn.innerHTML = ''
        @shd.innerHTML = ''
        
    ###
     0000000   000   000  000   000   0000000   000000000   0000000   000000000  00000000  
    000   000  0000  000  0000  000  000   000     000     000   000     000     000       
    000000000  000 0 000  000 0 000  000   000     000     000000000     000     0000000   
    000   000  000  0000  000  0000  000   000     000     000   000     000     000       
    000   000  000   000  000   000   0000000      000     000   000     000     00000000  
    ###
    
    annotate: ->

        @showLastMove()
        @numbers()
        @liberties()
        @territory()
        @game.updateTitle()
    
    # 000   000  000   000  00     00  0000000    00000000  00000000    0000000  
    # 0000  000  000   000  000   000  000   000  000       000   000  000       
    # 000 0 000  000   000  000000000  0000000    0000000   0000000    0000000   
    # 000  0000  000   000  000 0 000  000   000  000       000   000       000  
    # 000   000   0000000   000   000  0000000    00000000  000   000  0000000   
    
    numbers: ->
        
        @num.innerHTML = ''
        return if not @game
            
        s = @div.getBoundingClientRect().height/(@size+1)
        s = max 16 int s/3
        log 'num' s
        
        for m in @game.moves.m
            continue if m.pos in ['pass''resign']
            n = @game.moves.m.indexOf m
            c = @game.coord m.pos
            l = elem 'div' class:"number #{m.color}" parent:@num, text:1+n
            p = @coordToPrcnt c
            l.style = "left:#{p.x}%; top:#{p.y}%;" 
            l.style.fontSize = "#{int(2*s/3)}px"
            l.style.width = "#{s}px"
            l.style.height = "#{s}px"
            l.style.borderRadius = "#{s}px"
            if stone.empty == @game.stoneAt m.pos
                l.style.backgroundColor = m.color
                l.classList.add 'empty'
                    
    onVariation: (variation) =>

        return if not @game
                
        s = @div.getBoundingClientRect().height/(@size+1)
        s = max 16 s/3
        
        color = @game.nextColor()
        for m in variation
            n = variation.indexOf m
            c = @game.coord m
            l = elem 'div' class:"number #{color}" parent:@num, text:1+n
            p = @coordToPrcnt c
            l.style = "left:#{p.x}%; top:#{p.y}%;" 
            l.style.fontSize = "#{2*s/3}px"
            l.style.width = "#{s}px"
            l.style.height = "#{s}px"
            l.style.borderRadius = "#{s}px"
            if stone.empty == @game.stoneAt m
                l.style.backgroundColor = color
            color = opponent[color]
        
    # 000      000  0000000    00000000  00000000   000000000  000  00000000   0000000  
    # 000      000  000   000  000       000   000     000     000  000       000       
    # 000      000  0000000    0000000   0000000       000     000  0000000   0000000   
    # 000      000  000   000  000       000   000     000     000  000            000  
    # 0000000  000  0000000    00000000  000   000     000     000  00000000  0000000   
    
    liberties: ->
        
        @lib.innerHTML = ''
        return if not @game
                
        s = @div.getBoundingClientRect().height/(@size+1)
        s = min 15 s/6
        
        for color in ['black' 'white']
            for st in @game.allStones color
                c = @game.coord st
                libs = @game.liberties c
                if libs == 1 and @showTerritory âžœ continue
                c[0]+= 0.13
                c[1]+= 0.1
                p = @coordToPrcnt c
                l = elem 'div' class:"liberty #{color}" parent:@lib, text:libs, style: "left:#{p.x}%; top:#{p.y}%; font-size: #{s}px;" 

    # 000000000  00000000  00000000   00000000   000  000000000   0000000   00000000   000   000  
    #    000     000       000   000  000   000  000     000     000   000  000   000   000 000   
    #    000     0000000   0000000    0000000    000     000     000   000  0000000      00000    
    #    000     000       000   000  000   000  000     000     000   000  000   000     000     
    #    000     00000000  000   000  000   000  000     000      0000000   000   000     000     
    
    territory: ->
        
        @trr.innerHTML = ''
        if @game
            if @game.moves.num() > 1
                            
                for a in @game.areas
                    if a.color in 'wbWB'
                        for p in a.posl
                            e = elem 'div' class:"eye #{a.color}" parent:@trr
                            r = @coordToPrcnt @game.coord p
                            s = 22/(@size+1)
                            s = s.toFixed 2
                            e.style = "left:#{r.x}%; top:#{r.y}%; width:#{s}%; height:#{s}%;" 
                    
                for g in @game.grps
                    if g.state == 'dead'
                        for p in g.posl
                            e = elem 'div' class:"eye #{opponent[stoneColor[g.stone]][0]}" parent:@trr
                            r = @coordToPrcnt @game.coord p
                            s = 22/(@size+1)
                            s = s.toFixed 2
                            e.style = "left:#{r.x}%; top:#{r.y}%; width:#{s}%; height:#{s}%;" 

module.exports = Board
