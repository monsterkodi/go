###
 0000000   0000000   00     00  00000000   000
000       000   000  000   000  000   000  000
000       000   000  000000000  00000000   000
000       000   000  000 0 000  000        000
 0000000   0000000   000   000  000        000
###

{ stone, opponent }  = require '../util/util'
{ args, childp, post } = require 'kxk'

function Compi

    @: (@game, @name, cmd, args) ->
        
        @msg = []
        
        @proc = childp.spawn cmd, args, shell:true, detached:true
        @proc.stdout.on 'data' @onData
        @proc.stderr.on 'data' @onDataError
        @proc.once 'exit' @onExit
        
    onExit: (signal) =>
      
      delete @proc
      log "#{@name} process exited #{signal}"

    newGame: (boardsize, @color, @handicap) ->

        # @send "time_settings 0 10 1"
        @send "boardsize #{boardsize}"
        @send "fixed_handicap #{handicap}" if @handicap > 1
        @send "komi 0"

    genmove: -> 
        # ●▸ genmove
        @send "genmove #{@color}"
        
    opponentMove: (p) ->

        @send "play #{opponent[@color]} #{p}"        
        # @send 'showboard'
        
    estimateScore: ->
        
        @send 'estimate_score'
                
    undo: -> 
        if @msg[-1]?.startsWith 'genmove' then @send 'undo'
        @send 'undo'
        # @send 'showboard'
        
    send: (m) -> @msg.push m; @proc.stdin.write m + '\n'

    onDataError: (chunk) =>
        
        data = String chunk
        # log data if "a b c d e f g h j k l m n o p q r s t" in data
        @onStderr data
        
    onStderr: ->
        
    onData: (chunk) =>
        
        data = String chunk
        
        # log @name, 'onData msgs' @msg, "'#{data}'"
        
        if @partial 
            data = @partial + data
            delete @partial
            
        answers = data.split '\n\n'
        
        @partial = answers.pop()

        for answer in answers
            
            if answer[0] == '='
                @ok @msg.shift(), answer[2..]
            else
                error @msg.shift(), answer[2..]
                            
    ok: (m, data) ->
            
        switch m.split(' ')[0]
            
            'genmove'
                
                # ●▪ genmove
                
                if not 'undo' in @msg
                    p = data.split('\n')[0]
                    p = 'pass' if p == 'PASS'
                    post.emit 'playerMove' p, @name
                
            'fixed_handicap'
                
                if @color == 'black'
                    for p in data.split ' '
                        @game.setStone @game.coord(p), stone.black
                    @game.moves.push "black #{data}"
                    
            'estimate_score'
                
                @game.setScore data.split(' ')[0]
                
            'final_score'
                log @name, 'final_score' "'#{data}'"
                @game.finalScore data
                
            'showboard'
                
                log data
                
            # else log @name, 'ok -- ignore' m, "'#{data}'"

module.exports = Compi
