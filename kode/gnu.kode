###
 0000000   000   000  000   000
000        0000  000  000   000
000  0000  000 0 000  000   000
000   000  000  0000  000   000
 0000000   000   000   0000000 
###

{ childp } = require 'kxk'

function GNU

    @: (@game) ->
        
        @msg = []
        @gnu = childp.spawn '/usr/local/bin/gnugo' ['--mode' 'gtp' '--level' '20']
        @gnu.stdout.on 'data' @onData
        
    newGame: (boardsize, @color, @handicap, genmove) ->
        
        @send "boardsize #{boardsize}"
        @send "fixed_handicap #{handicap}" if @handicap > 1
        if @color == 'white'
            @human = 'black'
            @send "genmove #{@color}" if genmove and @handicap > 1
        else
            @human = 'white'
            @send "genmove #{@color}" if genmove and @handicap < 2
            
    humanMove: (p) ->

        delete @redos
        @game.play @human, p
        @send "play #{@human} #{p}"
        @calcscore()
        @send "genmove #{@color}"
        
    calcscore: ->
        
        @send 'estimate_score'

    undo: ->
        
        return if @game.moves.length < 2
        @send 'undo'
        @send 'undo'
        @redos ?= []
        @redos.unshift @game.moves.pop()
        @redos.unshift @game.moves.pop()
        @send 'showboard'
        @calcscore()
        
    redo: ->
        
        return if empty @redos
        move = @redos.shift()
        [color, p] = move.split ' '
        @game.play color, p
        @send "play #{color} #{p}"
        return if empty @redos
        move = @redos.shift()
        [color, p] = move.split ' '
        @game.play color, p
        @send "play #{color} #{p}"
        
    send: (m) -> @msg.push m; @gnu.stdin.write m + '\n'

    onData: (chunk) =>
        
        data = String chunk
        
        # log data
        # log @msg
        
        if @partial 
            data = @partial + data
            delete @partial
            
        while data.startsWith '= \n\n'
            data = data[4..]
            @msg.shift()

        if not data.endsWith '\n\n'
            @partial = data
            return
            
        if data[0] == '='
            m = @msg.shift()
            data = data[2..]
            # log m, data
            if m.startsWith 'genmove'
                p = data.split('\n')[0]
                # log 'play' @color, p
                if p in ['PASS' 'resign']
                    @send 'final_score'
                @game.play @color, p
                @game.calcScore()
            else if m.startsWith 'final_score'
                log m, data
                @game.finalScore data
            else if m.startsWith 'fixed_handicap'
                for p in data.split ' '
                    @game.setStone @game.coord(p), 'black'
                @game.dump()
            else if m.startsWith 'estimate_score'
                @game.setScore data.split(' ')[0]
            else if m.startsWith 'showboard'
                @game.show data
            else
                log m, data

module.exports = GNU
