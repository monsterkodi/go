###
 0000000   000   000  000      000  000   000  00000000
000   000  0000  000  000      000  0000  000  000     
000   000  000 0 000  000      000  000 0 000  0000000 
000   000  000  0000  000      000  000  0000  000     
 0000000   000   000  0000000  000  000   000  00000000
###

{ elem, noon, open, post, slash } = require 'kxk'
{ rank, rankToKyu, ogsMoves, ogsMove, toOGS, iconUrl } = require './util/util'
{ request } = require 'https'
{ io } = require 'socket.io-client'

Board = require './board'
Game = require './game'

function Online

    @: (@parent, @referee) ->

        @boards = {}
        @chats  = {}

        @postSecret()
                     
        post.on 'submitMove' @submitMove
        post.on 'loadGame'   @loadGame
        post.on 'resize'     @onResize
        post.on 'talk'       @onTalk
        
        @games = elem 'div' class:'games' parent:@parent 
        @games.addEventListener 'mousewheel' @onMouseWheel, true
        
        if not window.stash.get 'games' true
            @toggleGames()
        
    submitMove: (gameId, pos) =>
        
        if not @referee.tree.isAtNextMove() 
            log 'skip submitting variation move!' gameId, pos
            return
        
        move = toOGS pos, @boards[gameId].game.size
        @socket.emit 'game/move',
            auth:       @myAuth
            player_id:  @myUserId
            game_id:    gameId
            move:       move
        
    #  0000000   0000000    0000000  000   000  00000000  000000000  
    # 000       000   000  000       000  000   000          000     
    # 0000000   000   000  000       0000000    0000000      000     
    #      000  000   000  000       000  000   000          000     
    # 0000000    0000000    0000000  000   000  00000000     000     
    
    initSocket: (config) ->

        clockDrift = 0
        latency = 0
        
        global.myUserId    = @myUserId   = config.user.id
        global.myUserName  = @myUserName = config.user.username 
        global.myAuth      = @myAuth     = config.chat_auth
        global.notAuth     = @myNotAuth  = config.notification_auth
        
        ping = =>
            if @socket.connected then @socket.emit 'net/ping' client:Date.now(), drift:clockDrift, latency:latency
        
        pong = (data) =>
            now = Date.now()
            latency = now - data.client
            clockDrift = now - latency / 2 - data.server

        notification = =>
            @socket.emit 'notification/connect' player_id:@myUserId, auth:@myNotAuth
            @socket.emit 'chat/connect'         player_id:@myUserId, auth:@myAuth, username:@myUserName, ranking:20, ui_class:''
            
        authenticate = =>
            @socket.emit 'authenticate',
                auth:             config.chat_auth
                player_id:        config.user.id
                username:         config.user.username 
                jwt:              config.user_jwt      
                client_version:   config.version
            notification()

        @socket = io 'https://online-go.com', 
            reconnection: true
            reconnectionDelay: 750
            reconnectionDelayMax: 10000
            transports: ["websocket"]
            upgrade: false
            
        @socket.on 'net/pong' pong
        @socket.on 'connect_error'       => log 'on connect_error'
        @socket.on 'disconnect' (reason) => log 'on disconnect' reason
        
        @socket.on 'connect'  ping
        @socket.on 'connect'  authenticate
        @socket.on 'connect'  notification
            
        setInterval ping, 10000
        
        @socket.onAny (msg, arg) =>
            
            switch msg.split('/')[0]
            
                'nofification'  ➜ # if arg.type == 'gameEnded' then @getGames()
                'active_game'   
                                    log 'active_game:' arg.id #, noon arg # if not @boards[arg.id]
                                    @socket.emit 'game/connect',
                                        game_id:    arg.id
                                        player_id:  @myUserId
                                        chat:       1
                                                                                
                'game'          ➜   @onGameData msg, arg
                'score-estimator-enabled-state' 
                'active-bots' 
                'net' 
                'automatch'     ➜   ''
                                ➜   log 'any' msg, noon arg
                
    #  0000000   000   000         0000000    0000000   00     00  00000000        0000000     0000000   000000000   0000000   
    # 000   000  0000  000        000        000   000  000   000  000             000   000  000   000     000     000   000  
    # 000   000  000 0 000        000  0000  000000000  000000000  0000000         000   000  000000000     000     000000000  
    # 000   000  000  0000        000   000  000   000  000 0 000  000             000   000  000   000     000     000   000  
    #  0000000   000   000         0000000   000   000  000   000  00000000        0000000    000   000     000     000   000  
    
    onGameData: (msg, arg)->
        
        gameid = arg.game_id
        msgtyp = msg.split('/')[-1]
        
        switch msgtyp
            
            'move'

                pos = ogsMove arg.move, @boards[gameid].game.size
                log 'move:' arg
                
                b = @boards[gameid]
                b.game.play pos
                
                @updateMyMove b
                        
                log 'load game?' @referee.game.info.id, gameid, @referee.game.info.id == gameid
                if @referee.game.info.id == gameid
                    @loadGame gameid
            
            'chat'
                # log 'chat' msg, noon arg
                gameid = int msg.split('/')[-2]
                line = arg.line
                line.color = line.username == @myUserName ? 'myself' : 'black'
                line.gameid = gameid
                @chats[gameid] ?= {}
                @chats[gameid][line.date] = line
                post.emit 'chat' line, 1
                
            'clock'
            'conditional_moves'
            'reset-chats'       ➜ ''
            'gamedata'          
                                  # log 'gamedata:' msg, msgtyp, gameid, noon arg
                                  if not @boards[gameid] then @addBoard arg
                                ➜ log 'game:' msg, noon arg

    onTalk: (msg) =>
        
        @socket.emit 'game/chat',
            body:       msg
            type:       'main'
            game_id:     @referee.game.info.id
            move_number: @referee.game.moves.num()
    
    # 00     00  000   000        00     00   0000000   000   000  00000000  
    # 000   000   000 000         000   000  000   000  000   000  000       
    # 000000000    00000          000000000  000   000   000 000   0000000   
    # 000 0 000     000           000 0 000  000   000     000     000       
    # 000   000     000           000   000   0000000       0      00000000  
    
    updateMyMove: (b) ->
        
        if b.game.players[b.game.nextColor()] == global.myUserName # it's my turn!
            b.div.style.border = '2px solid black'
            b.div.style.borderRadius = '6px'
            e = b.parent # move board to top
            t = e.previousElementSibling
            e.parentElement.insertBefore e, e.parentElement.firstChild
            t.parentElement.insertBefore t, t.parentElement.firstChild
            t.scrollIntoViewIfNeeded()
        else
            b.div.style.border = 'none'
                                
    #  0000000  00000000   0000000  00000000   00000000  000000000  
    # 000       000       000       000   000  000          000     
    # 0000000   0000000   000       0000000    0000000      000     
    #      000  000       000       000   000  000          000     
    # 0000000   00000000   0000000  000   000  00000000     000     
    
    postSecret: ->
        
        secret = noon.load slash.resolve "#{__dirname}/../bin/.secret"
        body   = "client_id=#{secret.client_id}&client_secret=#{secret.client_secret}&username=#{secret.username}&password=#{secret.password}&grant_type=password"

        @post path:'/oauth2/token/' body:body, cb: (d) =>
            if d.access_token
                @token = d.access_token
                # if window.stash.get 'games' true
                    # @showGames()
                @get path:'/api/v1/ui/config' cb: (d) =>
                    @initSocket d
            else
                log 'no token!'
                                            
    #  0000000   0000000    0000000          0000000     0000000    0000000   00000000   0000000    
    # 000   000  000   000  000   000        000   000  000   000  000   000  000   000  000   000  
    # 000000000  000   000  000   000        0000000    000   000  000000000  0000000    000   000  
    # 000   000  000   000  000   000        000   000  000   000  000   000  000   000  000   000  
    # 000   000  0000000    0000000          0000000     0000000   000   000  000   000  0000000    
    
    addBoard: (game) ->
        
        # log 'addBoard' noon game
        
        g = elem 'div' class:'game' parent:@games
        
        for color in ['black' 'white']
            p = game.players[color] 
            if p.username != 'monsterkodi'
                ib = elem 'img'  parent:g, class:"gameIcon #{color}" 
                rb = elem 'span' parent:g, class:"gameRank #{color}" text:rankToKyu p.rank
                nb = elem 'span' parent:g, class:"gameName #{color}" text:p.username
                ib.addEventListener 'click' ((id) -> -> open 'https://online-go.com/game/'+id) game.game_id
                @get path:"/api/v1/players/#{p.id}" cb:((ib) => (p) => ib.src = iconUrl p.icon, 128 ) ib

        e = elem 'div' class:'gameboard' parent:@games

        features = 
            coordinates: false
            liberties:   false
            territory:   false
            numbers:     false
            hover:       false
            dots:        false
        
        b = new Board e, game.height, features
        b.game = new Game b, game.players.black.username, game.players.white.username, game.handicap
        b.game.paused = true
        b.game.info.id = game.game_id
        b.game.info.komi = game.komi
        b.game.replay ogsMoves(game.moves, game.height), true
        
        br = @parent.getBoundingClientRect()
        tb = br.height / (@referee.boardsize+1) - 2
        rb = tb
        w  = max 128 (br.width - br.height)/2-tb

        b.div.style.height = "#{w-30}px"
        b.div.style.width  = "#{w-30}px"
        b.div.addEventListener 'click' ((id) => => @loadGame id) game.game_id
        @boards[game.game_id] = b
        b.onResize()
        
        @updateMyMove b
                    
    # 000       0000000    0000000   0000000           0000000    0000000   00     00  00000000  
    # 000      000   000  000   000  000   000        000        000   000  000   000  000       
    # 000      000   000  000000000  000   000        000  0000  000000000  000000000  0000000   
    # 000      000   000  000   000  000   000        000   000  000   000  000 0 000  000       
    # 0000000   0000000   000   000  0000000           0000000   000   000  000   000  00000000  
    
    loadGame: (id) =>
        
        if b = @boards[id]
            g = b.game
            @referee.newGame
                black:    g.players.black
                white:    g.players.white
                size:     g.size
                handicap: g.handicap
                
            moves = g.moves.m
            
            @referee.game.paused = true
            @referee.game.info.id = id
            @referee.game.info.komi = g.info.komi
            @referee.game.replay moves, true
            @referee.tree.replay moves, id
            @referee.game.estimate()
            @referee.board.annotate()
            
            for k,b of @boards
                t = b.parent.previousElementSibling
                t.style.backgroundColor = 'transparent'
                t.style.boxShadow = 'unset'
            if e = @boards[id]?.parent
                t = e.previousElementSibling
                t.style.backgroundColor = 'rgba(80,30,0,0.03)'
                t.style.boxShadow       = 'inset 1px 1px 6px rgba(80,30,0,0.6)'
                
                t.scrollIntoViewIfNeeded()
                e.scrollIntoViewIfNeeded()

        clearPending = => delete @pendingGameId
        delete @pendingGameId
        @pendingGameId = int id if id is num             
        @get path:"/api/v1/games/#{id}" cb: (g) =>
            
            if g.id != @pendingGameId and @pendingGameId
                setTimeout clearPending, 4000        
                log 'ignoring non-pending game data' @pendingGameId, g.id
                return
                
            delete @pendingGameId
            @referee.newGame
                black:    g.gamedata.players.black.username
                white:    g.gamedata.players.white.username
                size:     g.gamedata.width
                handicap: g.gamedata.handicap
                
            moves = ogsMoves g.gamedata.moves, g.gamedata.height
            @referee.game.paused = true
            @referee.game.info.id = g.id
            @referee.game.info.komi = g.gamedata.komi
            @referee.game.replay moves, true
            @referee.tree.replay moves, g.id
            @referee.game.estimate()
            @referee.board.annotate()
            
            @boards[id]?.div.scrollIntoViewIfNeeded()
            
            for date,line of @chats[id]
                post.emit 'chat' line
            @referee.varee.fixChat()
            
    nextGame: ->

        ids = Object.keys @boards
        if @referee.game.info.id
            for i in 0...ids.length
                if int(ids[i]) == @referee.game.info.id
                    id = int ids[clamp 0, ids.length-1, i+1]
                    break

        id ?= int ids[0]
        # log 'nextGame id' id
        @loadGame id
        
    prevGame: ->

        ids = Object.keys @boards
        if @referee.game.info.id
            for i in 0...ids.length
                if int(ids[i]) == @referee.game.info.id
                    id = int ids[clamp 0, ids.length-1, i-1]
                    break

        id ?= int ids[0]
        # log 'prevGame id' id
        @loadGame id
        
    # 00000000    0000000    0000000  000000000  
    # 000   000  000   000  000          000     
    # 00000000   000   000  0000000      000     
    # 000        000   000       000     000     
    # 000         0000000   0000000      000     
    
    post: (o) ->
        
        req = request {
                host: 'online-go.com'
                path: o.path
                method: 'POST'
                headers: 'Content-Type': 'application/x-www-form-urlencoded'
            }, (response) =>
                log 'status:' response.statusCode if response.statusCode != 200
                response.setEncoding 'utf8'
                response.on 'data' (s) =>
                    d = JSON.parse s
                    o.cb(d) if o.cb is func
                      
        req.on 'error' (e) -> log 'post error' e        
        req.write o.body if o.body
        req.end()
          
    #  0000000   00000000  000000000  
    # 000        000          000     
    # 000  0000  0000000      000     
    # 000   000  000          000     
    #  0000000   00000000     000     
    
    get: (o) ->
        
        # log noon o
        req = request {
                host: 'online-go.com'
                path: o.path
                method: 'GET'
                headers: 
                    'Authorization': "Bearer #{@token}"
                    'Content-Type': 'application/x-www-form-urlencoded'
            }, (response) =>
                if response.statusCode != 200
                    log 'not ok:' response.statusCode, '(429=throttled)'
                    return
                response.setEncoding 'utf8'
                data = ""
                response.on 'data' (chunk) => data += chunk
                response.on 'end' =>
                    d = JSON.parse data
                    o.cb(d) if o.cb is func
                      
        req.on 'error' (e) -> log 'get error' e
        req.end()
        
    # 000000000   0000000    0000000    0000000   000      00000000         0000000    0000000   00     00  00000000   0000000  
    #    000     000   000  000        000        000      000             000        000   000  000   000  000       000       
    #    000     000   000  000  0000  000  0000  000      0000000         000  0000  000000000  000000000  0000000   0000000   
    #    000     000   000  000   000  000   000  000      000             000   000  000   000  000 0 000  000            000  
    #    000      0000000    0000000    0000000   0000000  00000000         0000000   000   000  000   000  00000000  0000000   
    
    toggleGames: ->

        if @games.style.display != 'none'
            # @games.remove()
            @games.style.display = 'none'
            # delete @games
            window.stash.set 'games' false
        else
            window.stash.set 'games' true
            @showGames()
            
    showGames: =>
            
        @games.style.display = 'initial'
        @onResize()
                
    # 00000000   00000000   0000000  000  0000000  00000000  
    # 000   000  000       000       000     000   000       
    # 0000000    0000000   0000000   000    000    0000000   
    # 000   000  000            000  000   000     000       
    # 000   000  00000000  0000000   000  0000000  00000000  
    
    onResize: =>
        
        return if not @games
        
        br = @parent.getBoundingClientRect()
        tb = br.height / (@referee.boardsize+1) - 2
        w  = max 128 (br.width - br.height)/2-tb
        lb = (br.width - br.height)/2 - w

        if (br.width - br.height)/2 < 128
            @games.style.display = 'none'
        else
            @games.style.display = 'initial'
        
        @games.style.width  = "#{w}px"
        @games.style.top    = "#{tb}px"
        @games.style.bottom = "#{tb}px"
        @games.style.left   = "#{lb}px"
        
        for i,b of @boards
            b.div.style.width  = "#{w-30}px"
            b.div.style.height = "#{w-30}px"

    onMouseWheel: (event) => 
        # prevent tree navigation when mouse wheeling over games list
        event.stopPropagation()
            
module.exports = Online
