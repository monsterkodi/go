###
00000000   00000000  00000000  00000000  00000000   00000000  00000000  
000   000  000       000       000       000   000  000       000       
0000000    0000000   000000    0000000   0000000    0000000   0000000   
000   000  000       000       000       000   000  000       000       
000   000  00000000  000       00000000  000   000  00000000  00000000  
###

kxk = require 'kxk'
{ elem, post } = kxk
{ opponent }   = require './util'

Leelaz  = require './leelaz'
Katago  = require './katago'
Hara    = require './hara'
Board   = require './board'
Game    = require './game'
GNU     = require './gnu'
SGF     = require './sgf'

function Referee

    @: (@parent)->
        
        @boardsize = window.stash.get 'boardsize'   19
        @handicap  = window.stash.get 'handicap'    0
        @white     = window.stash.get 'white'       'human'
        @black     = window.stash.get 'black'       'human'
        
        post.on 'newGame'    @newGame
        post.on 'playerMove' @playerMove

    # 000   000  00000000  000   000         0000000    0000000   00     00  00000000  
    # 0000  000  000       000 0 000        000        000   000  000   000  000       
    # 000 0 000  0000000   000000000        000  0000  000000000  000000000  0000000   
    # 000  0000  000       000   000        000   000  000   000  000 0 000  000       
    # 000   000  00000000  00     00         0000000   000   000  000   000  00000000  
    
    newGame: (gi={}) =>

        # log noon gi
        
        @white     = gi.white    ? @white
        @black     = gi.black    ? @black
        @handicap  = gi.handicap ? @handicap
        @boardsize = gi.size     ? @boardsize
        moves      = gi.moves    ? []
        info       = gi.info     ? {}
        @redos     = []
        
        if @white == 'leelaz' or @black == 'leelaz' then @boardsize = 19
        
        # log @boardsize, @handicap, info, gi
        
        window.stash.del 'score' if empty moves
        window.stash.set 'size'      @boardsize
        window.stash.set 'white'     @white
        window.stash.set 'black'     @black
        window.stash.set 'handicap'  @handicap
        window.stash.set 'moves'     moves
                
        @parent.innerHTML = ''
                
        @board = new Board @parent, @boardsize
        @game  = new Game  @board, @white, @black, @handicap
        @board.game = @game
        @game.info  = info
        
        @compi = {}
                    
        switch @black
            'gnu'       ➜ @compi.black = new GNU    @game
            'leelaz'    ➜ @compi.black = new Leelaz @game
            'hara'      ➜ @compi.black = new Hara   @game
            'katago'    ➜ @compi.black = new Katago @game
            
        switch @white
            'gnu'       ➜ @compi.white = new GNU    @game
            'leelaz'    ➜ @compi.white = new Leelaz @game
            'hara'      ➜ @compi.white = new Hara   @game
            'katago'    ➜ @compi.white = new Katago @game
                
        @compi.black?.newGame @boardsize, 'black' @handicap
        @compi.white?.newGame @boardsize, 'white' @handicap
        
        if valid moves
            @replay moves
        else
            if @compi.black and @handicap < 2
                @compi.black.genmove()
                
            if @compi.white and @handicap > 1
                @compi.white.genmove()
            
    # 00000000   00000000  00000000   000       0000000   000   000  
    # 000   000  000       000   000  000      000   000   000 000   
    # 0000000    0000000   00000000   000      000000000    00000    
    # 000   000  000       000        000      000   000     000     
    # 000   000  00000000  000        0000000  000   000     000     
    
    replay: (moves) ->
            
        for m in moves
            [c, p] = m.split ' '
            if not p?
                p = c
                c = ['black' 'white'][moves.indexOf(m)%2]
            @game.play c, p
            if 1
                @compi.black?.send "play #{c} #{p}"
                @compi.white?.send "play #{c} #{p}"
        
        score = @game.info.score ? window.stash.get 'score'

        if score
            @game.finalScore score
        else
            @compi[@game.nextColor()]?.genmove()

    # 00     00   0000000   000   000  00000000  
    # 000   000  000   000  000   000  000       
    # 000000000  000   000   000 000   0000000   
    # 000 0 000  000   000     000     000       
    # 000   000   0000000       0      00000000  
    
    genMove: -> @game.genmove @game.nextColor()
    
    playerMove: (p, player) =>
        
        lastColor = @game.lastColor()
        nextColor = @game.nextColor()
        
        return error("wrong player: #{player}", nextColor, @game.players) if @game.players[nextColor] != player
        
        @game.play nextColor, p
        @compi[lastColor]?.opponentMove p
            
        if p in ['pass' 'resign']
            @compi.black?.send 'final_score'
            @compi.white?.send 'final_score'
            
        if p != 'resign'
            if p != 'pass' or 'pass' not in @game.moves[-2]
                # log "#{lastColor} genmove" @compi[lastColor]?
                @compi[lastColor]?.genmove()
                            
    # 000   000  000   000  0000000     0000000   
    # 000   000  0000  000  000   000  000   000  
    # 000   000  000 0 000  000   000  000   000  
    # 000   000  000  0000  000   000  000   000  
    #  0000000   000   000  0000000     0000000   
    
    undo: ->
        
        return if empty @game.moves
        return if @game.handicap > 1 and @game.moves.length == 1
        
        return if valid @compi.black?.msg
        return if valid @compi.white?.msg
        
        # log 'undo' @game.moves
        
        @redos ?= []
        while @game.moves[-1].split(' ')[1] in ['pass' 'resign']
            @redos.unshift @game.moves.pop() 
            @compi.black?.send 'undo'
            @compi.white?.send 'undo'
            
        @redos.unshift @game.moves.pop()
        
        # log 'undo' @game.moves, @redos
        
        @compi.black?.undo()
        @compi.white?.undo()
            
    redo: ->
        
        return if empty @redos
        return if valid @compi.black?.msg
        return if valid @compi.white?.msg
        
        move = @redos.shift()
        [color, p] = move.split ' '
        @game.play color, p
        @compi.black?.send "play #{color} #{p}"
        @compi.white?.send "play #{color} #{p}"
            
    # 00000000  000  00000000    0000000  000000000        000       0000000    0000000  000000000  
    # 000       000  000   000  000          000           000      000   000  000          000     
    # 000000    000  0000000    0000000      000           000      000000000  0000000      000     
    # 000       000  000   000       000     000           000      000   000       000     000     
    # 000       000  000   000  0000000      000           0000000  000   000  0000000      000    
    
    firstMove: ->
        
        return if empty @game.moves
        @redos = @game.moves.concat @redos
        @compi.black?.send "boardsize #{@game.size}"
        @compi.white?.send "boardsize #{@game.size}"
        @game.clear_board()
        
    lastMove: ->

        return if empty @redos
        while valid @redos
            move = @redos.shift()
            [color, p] = move.split ' '
            @game.play color, p
            @compi.black?.send "play #{color} #{p}"
            @compi.white?.send "play #{color} #{p}"
                    
module.exports = Referee
