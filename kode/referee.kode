###
00000000   00000000  00000000  00000000  00000000   00000000  00000000  
000   000  000       000       000       000   000  000       000       
0000000    0000000   000000    0000000   0000000    0000000   0000000   
000   000  000       000       000       000   000  000       000       
000   000  00000000  000       00000000  000   000  00000000  00000000  
###

kxk = require 'kxk'
{ elem, post } = kxk
{ opponent }   = require './util'

Leelaz  = require './leelaz'
Board   = require './board'
Game    = require './game'
GNU     = require './gnu'
SGF     = require './sgf'

function Referee

    @: (@parent)->
        
        @boardsize = window.stash.get 'boardsize'   19
        @handicap  = window.stash.get 'handicap'    0
        @white     = window.stash.get 'white'       'human'
        @black     = window.stash.get 'black'       'human'
        
        post.on 'newGame'    @newGame
        post.on 'playerMove' @playerMove

    # 000   000  00000000  000   000         0000000    0000000   00     00  00000000  
    # 0000  000  000       000 0 000        000        000   000  000   000  000       
    # 000 0 000  0000000   000000000        000  0000  000000000  000000000  0000000   
    # 000  0000  000       000   000        000   000  000   000  000 0 000  000       
    # 000   000  00000000  00     00         0000000   000   000  000   000  00000000  
    
    newGame: (gi={}) =>

        # log noon gi
        
        @white     = gi.white    ? @white
        @black     = gi.black    ? @black
        @handicap  = gi.handicap ? @handicap
        @boardsize = gi.size     ? @boardsize
        moves      = gi.moves    ? []
        info       = gi.info     ? {}
        @redos     = []
        
        if @white == 'leelaz' or @black == 'leelaz' then @boardsize = 19
        
        log @boardsize, @handicap, info, gi
        
        window.stash.del 'score' if empty moves
        window.stash.set 'size'      @boardsize
        window.stash.set 'white'     @white
        window.stash.set 'black'     @black
        window.stash.set 'handicap'  @handicap
        window.stash.set 'moves'     moves
                
        @parent.innerHTML = ''
                
        @board = new Board @parent, @boardsize
        @game  = new Game  @board, @white, @black, @handicap
        @board.game = @game
        @game.info  = info
        
        @gnu = {}
        
        if @white == 'gnu'
            @gnu.white = new GNU @game
            @gnu.white.newGame @boardsize, 'white' @handicap
            
        if @white == 'leelaz'
            @gnu.white = new Leelaz @game
            @gnu.white.newGame @boardsize, 'white' @handicap
            
        if @black == 'gnu'
            @gnu.black = new GNU @game
            @gnu.black.newGame @boardsize, 'black' @handicap

        if @black == 'leelaz'
            @gnu.black = new Leelaz @game
            @gnu.black.newGame @boardsize, 'black' @handicap
            
        if valid moves
            @replay moves
        else
            if @gnu.black and @handicap < 2
                @gnu.black.genmove()
                
            if @gnu.white and @handicap > 1
                @gnu.white.genmove()
            
    # 00000000   00000000  00000000   000       0000000   000   000  
    # 000   000  000       000   000  000      000   000   000 000   
    # 0000000    0000000   00000000   000      000000000    00000    
    # 000   000  000       000        000      000   000     000     
    # 000   000  00000000  000        0000000  000   000     000     
    
    @replay: (moves) ->
            
        for m in moves
            [c, p] = m.split ' '
            if not p?
                p = c
                c = ['black' 'white'][moves.indexOf(m)%2]
            @game.play c, p
            @gnu.black?.send "play #{c} #{p}"
            @gnu.white?.send "play #{c} #{p}"
        
        score = info.score ? window.stash.get 'score'

        if score
            @game.finalScore score
        else
            @gnu[@game.nextColor()]?.genmove()

    # 00     00   0000000   000   000  00000000  
    # 000   000  000   000  000   000  000       
    # 000000000  000   000   000 000   0000000   
    # 000 0 000  000   000     000     000       
    # 000   000   0000000       0      00000000  
    
    genMove: -> @game.genmove @game.nextColor()
    
    playerMove: (p, player) =>
        
        lastColor = @game.lastColor()
        nextColor = @game.nextColor()
        
        return error("wrong player: #{player}", nextColor, @game.players) if @game.players[nextColor] != player
        
        @game.play nextColor, p
        @gnu[lastColor]?.opponentMove p
            
        if p in ['pass' 'resign']
            @gnu.black?.send 'final_score'
            @gnu.white?.send 'final_score'
            
        if p != 'resign'
            if p != 'pass' or 'pass' not in @game.moves[-2]
                @gnu[lastColor]?.genmove()
                            
    # 000   000  000   000  0000000     0000000   
    # 000   000  0000  000  000   000  000   000  
    # 000   000  000 0 000  000   000  000   000  
    # 000   000  000  0000  000   000  000   000  
    #  0000000   000   000  0000000     0000000   
    
    undo: ->
        
        return if empty @game.moves
        return if @game.handicap > 1 and @game.moves.length == 1
        
        return if valid @gnu.black?.msg
        return if valid @gnu.white?.msg
        
        # log 'undo' @game.moves
        
        @redos ?= []
        while @game.moves[-1].split(' ')[1] in ['pass' 'resign']
            @redos.unshift @game.moves.pop() 
            @gnu.black?.send 'undo'
            @gnu.white?.send 'undo'
            
        @redos.unshift @game.moves.pop()
        
        # log 'undo' @game.moves, @redos
        
        @gnu.black?.undo()
        @gnu.white?.undo()
            
    redo: ->
        
        return if empty @redos
        return if valid @gnu.black?.msg
        return if valid @gnu.white?.msg
        
        move = @redos.shift()
        [color, p] = move.split ' '
        @game.play color, p
        @gnu.black?.send "play #{color} #{p}"
        @gnu.white?.send "play #{color} #{p}"
            
    # 00000000  000  00000000    0000000  000000000        000       0000000    0000000  000000000  
    # 000       000  000   000  000          000           000      000   000  000          000     
    # 000000    000  0000000    0000000      000           000      000000000  0000000      000     
    # 000       000  000   000       000     000           000      000   000       000     000     
    # 000       000  000   000  0000000      000           0000000  000   000  0000000      000    
    
    firstMove: ->
        
        return if empty @game.moves
        @redos = @game.moves.concat @redos
        @gnu.black?.send "boardsize #{@game.size}"
        @gnu.white?.send "boardsize #{@game.size}"
        @game.clear_board()
        
    lastMove: ->

        return if empty @redos
        while valid @redos
            move = @redos.shift()
            [color, p] = move.split ' '
            @game.play color, p
            @gnu.black?.send "play #{color} #{p}"
            @gnu.white?.send "play #{color} #{p}"
                    
module.exports = Referee
